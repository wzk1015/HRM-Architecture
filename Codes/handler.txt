.addr data_base 0x000
.addr addr_inbox 0x880
.addr addr_outbox 0x882
.addr kernel_text_base 0x4000
.addr pow_2_15 0x82c
.addr pow_2_14 0x82a
.addr pow_2_13 0x828
.addr pow_2_12 0x826
.addr pow_2_11 0x824
.addr zero 0x800
.addr one 0x802
.addr two 0x804
.addr three 0x806
.addr four 0x808
.addr five 0x80a
.addr six 0x80c
.addr seven 0x80e
.addr empty_value 0x880

.data
	reg_field:		.space 2 
	exception_counter:	.space 2
	exccode:		.space 2
	wrong_addr:	.space 2

.macro branch_exccode(%number, %label)
	copyfrom exccode
	sub %number
	jumpz %label
.end_macro

.text
	copyto reg_field 		# save register
	bumpup exception_counter	# update counter
	copyto exception_counter

	mfcause			#read exccode
	sub pow_2_15
	#sub pow_2_14		#remove exl. crash is already zero
	copyto exccode

	branch_exccode(one, overflow)	#branch based on exccode
	branch_exccode(two, empty_mem)
	branch_exccode(three, empty_reg)
	branch_exccode(four, unrec_instr)
	branch_exccode(five, permission_denied)
	branch_exccode(six, inbox_trap)
	branch_exccode(seven, outbox_trap)
	jump end				#unknown exception

crash:
	mfcause		#crash
	add pow_2_14
	mtcause
	jump end

overflow:
permission_denied:
	mfepc		#skip wrong instruction by adding 2 to EPC
	add two
	mtepc
	jump end

empty_mem:
	copyfrom zero	#load 0 to reg
	copyto reg_field
	jump end
	
empty_reg:
	copyfrom zero	#load 0 to reg
	copyto reg_field
	jump end

unrec_instr:
	jump end		#epc has been set to addr(jump/eret)+2 by hardware, so just jump

inbox_trap:
	copyfrom [addr_inbox]
	sub empty_value
	jumpz inbox_empty	#judge if inbox_addr is empty
	copyfrom [addr_inbox]
	jump end
	
inbox_empty:
	jump crash

outbox_trap:
	copyfrom reg_field
	sub empty_value
	jumpz crash	#if reg is empty, carsh
	copyfrom reg_field
	copyto [addr_outbox]
	copyfrom empty_value
	copyto reg_field
	jump end

end:
	copyfrom zero	#reset cause
	mtcause
	copyfrom reg_field	#recover register
	eret		#return